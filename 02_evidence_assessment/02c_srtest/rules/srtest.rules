
AUTOSOMES = [str(x) for x in range(1, 23)]
ALLOSOMES = 'X Y'.split()

def input_vcf(wildcards):
    path='{input_vcfs}/{batch}.{source}.{chrom}.vcf.gz'
    path = path.format(input_vcfs=config['input_vcfs'], batch=wildcards.batch,
                       source=wildcards.source, chrom=wildcards.chrom)
    return path

# TODO: generate blacklist from batch key
rule srtest_autosomes:
    input:
        vcf=input_vcf
    output:
        metrics='srtest/{batch}.{source}.{chrom}.stats',
    wildcard_constraints:
        chrom='(' + '|'.join(AUTOSOMES) + ')'
    shell:
         """
         svtools sr-test {input.vcf} {config[sr_counts]} {output.metrics}
         """

rule srtest_allosomes:
    input:
        vcf=input_vcf
    output:
        metrics='srtest_allosomes/{batch}.{source}.{chrom}.{sex}.stats',
    params:
        whitelist='whitelists/{batch}.{sex}.list'
    shell:
         """
         svtools sr-test --samples {params.whitelist} \
            {input.vcf} {config[sr_counts]} {output.metrics}
         """

rule merge_allosomes:
    input:
        males='srtest_allosomes/{batch}.{source}.{chrom}.males.stats',
        females='srtest_allosomes/{batch}.{source}.{chrom}.females.stats',
    output:
        metrics='srtest/{batch}.{source}.{chrom}.stats',
    wildcard_constraints:
        chrom='(' + '|'.join(ALLOSOMES) + ')'
    run:
        males = pd.read_table(input.males)
        females = pd.read_table(input.females)
        if males.shape[0] != females.shape[0]:
            raise Exception('mismatched table sizes')
        if wildcards.chrom == 'Y':
            males.to_csv(output.metrics, sep='\t', index=False)
        else:
            male_only = females.log_pval.isnull()
            females.loc[male_only] = males
            females.to_csv(output.metrics, sep='\t', index=False, na_rep='NA')
