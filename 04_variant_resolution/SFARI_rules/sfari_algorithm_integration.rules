"""
pesr_integration.rules
Matthew Stone <mstone5@mgh.harvard.edu>

Cluster SV after filtering based on PE, SR, RD, and BAF evidence. 
"""

import pandas as pd

PESR_SOURCES = config['pesr_sources']
with open(config['chroms']) as clist:
    CHROMS = [c.strip() for c in clist.readlines()]

localrules: all, make_vcf_list


def get_batch_vcfs(wildcards):
    """Collect all VCFs for a batch/chrom pairing across sources"""

    # print(wildcards)
    if wildcards.rf_status == 'pass':
        vcf_dir = config['filtered_vcfs']
    elif wildcards.rf_status == 'fail':
        vcf_dir = config['fail_vcfs']
    elif wildcards.rf_status == 'raw':
        vcf_dir = config['raw_vcfs']
    else:
        raise Exception('Invalid status: {0}'.format(wildcards.rf_status))

    vcf_path = os.path.join(vcf_dir, '{batch}.{source}.{chrom}.vcf.gz')

    if wildcards.evidence == 'pesr':
        return [vcf_path.format(batch=wildcards.batch, source=source,
                            chrom=wildcards.chrom)
                for source in PESR_SOURCES]

    elif wildcards.evidence == 'depth':
        return [vcf_path.format(**wildcards, source='depth')]

# Make list of all algorithm VCFs for each batch/chrom combo
rule make_vcf_list:
    input:
        get_batch_vcfs
    output:
        vcflist='vcflists/{evidence}/{rf_status}/{batch}.{chrom}.list'
    run:
        with open(output[0], 'w') as fout:
            for fname in sorted(input):
                fout.write(fname + '\n')

# Cluster VCFs across algorithms
rule merge_algorithms:
    """Cluster calls within a batch across algorithms"""
    input:
        vcflist=rules.make_vcf_list.output.vcflist
    output:
        vcf='vcfcluster/{evidence}/{rf_status}/{batch}.{chrom}.vcf.gz',
        tbi='vcfcluster/{evidence}/{rf_status}/{batch}.{chrom}.vcf.gz.tbi',
    params:
        vcfcluster=lambda wildcards: config['vcfcluster'][wildcards.evidence],
        prefix=lambda wildcards: '{batch}_{chrom}_{evidence}_{rf_status}'.format(**wildcards)
    shell:
        """
        svtools vcfcluster {input} stdout \
            -p {params.prefix} \
            -d {params.vcfcluster[dist]} \
            -f {params.vcfcluster[frac]} \
            -x {params.vcfcluster[blacklist]} \
            -z {params.vcfcluster[svsize]} \
            -t {params.vcfcluster[svtypes]} \
            --preserve-ids \
          | vcf-sort -c \
          | bgzip -c > {output.vcf};
        tabix -p vcf {output.vcf};
        """
